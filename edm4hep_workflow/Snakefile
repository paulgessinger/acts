finding_plots = expand("trackeff_vs_{q}", q=["eta", "pT", "phi"])\
            + ["nHoles_vs_eta", "nMeasurements_vs_eta", "nOutliers_vs_eta"]

fitting_plots = expand("reswidth_{q}_vs_eta", q=["d0", "z0", "qop", "phi", "theta", "t"])

samples = \
    expand("pg_mu_pt{pt}", pt=[1, 10, 100])\
  + ["mg_ttbar", "mg_dihiggs"]

event_counts = {
    "ttbar": 200,
    "dihiggs": 200,
}

def get_events(wildcards):
    print("get events:", wildcards)
    if hasattr(wildcards, "sample"):
        sample = wildcards.sample
        return event_counts[sample]
    
    if hasattr(wildcards, "particle"):
        return 1000

    raise ValueError("Cannot determine event count")

rule all:
  input: 
    #expand("reco/{sample}.edm4hep.root", sample=samples),
    #expand("reco/plots/{sample}/{plot}.pdf", sample=samples, plot=finding_plots+fitting_plots),
    #expand("reco/plots/{sample}/plots_{sample}.pdf", sample=samples),
    #"gen/mg_ttbar.hepmc3.zst",
    #"gen/mg_dihiggs.hepmc3.zst",
    #expand("gen/{sample}.hepmc3.zst", sample=samples),
    expand("sim/{sample}.edm4hep.root", sample=samples),
    #expand("digi/{sample}.edm4hep.root", sample=samples),

rule madgraph_init:
  output:
    "samples/mg_{sample}.tar.gz"
  input:
    "samples/{sample}.toml"
  # largely single thread but compilation goes up
  threads: workflow.cores
  shell:
    "./with_env.sh colliderml -vv madgraph init {input} {output}"

rule madgraph_gen:
  input:
    "samples/{sample}.toml",
    "samples/mg_{sample}.tar.gz"
  output:
    "gen/mg_{sample}.hepmc3.zst",
    "gen/mg_{sample}.hepmc3.zst.json"
  params:
    events=get_events
  threads: workflow.cores
  shell:
    "./with_env.sh colliderml -vv madgraph generate {input} --events {params.events} {output[0]}"

rule particle_gun:
  output:
    "gen/pg_{particle, [a-z]+}_pt{pt,\\d+}.hepmc3.zst",
    "gen/pg_{particle, [a-z]+}_pt{pt,\\d+}.hepmc3.zst.json"
  params:
    events=get_events
  threads: workflow.cores
  shell:
    "./with_env.sh colliderml particle-gun --jobs {threads} {output[0]} --type {wildcards.particle} --pt {wildcards.pt} --events {params.events}"

rule sim:
  input:
    "gen/{sample}.hepmc3.zst",
    "gen/{sample}.hepmc3.zst.json",
  output: 
    "sim/{sample}.edm4hep.root"
  params:
    minkin=0.001,
  threads: workflow.cores
  shell:
    "./with_env.sh colliderml -vv simulation {input[0]} --output {output[0]} --minimal-kinetic-energy {params.minkin} --processes {threads}"

rule digi:
  input:
    "sim/{sample}.edm4hep.root"
  output: 
    "digi/{sample}.edm4hep.root"
  threads: workflow.cores
  shell:
    "./with_env.sh colliderml digitization {input} {output} --jobs {threads} --config config.toml"

rule reco:
  input:
    "digi/{sample}.edm4hep.root"
  output: 
    "reco/{sample}.edm4hep.root",
    "reco/performance_finding_ckf_{sample}.root",
    "reco/performance_fitting_ckf_{sample}.root"
  threads: workflow.cores
  shell:
    "./with_env.sh python3 ./reconstruction.py {input} {output[0]} --jobs {threads} --loglevel INFO --config config.toml"

rule reco_plots:
  input:
    "reco/performance_finding_ckf_{sample}.root",
    "reco/performance_fitting_ckf_{sample}.root"
  output: 
    [f"reco/plots/{{sample}}/{plot}.pdf" for plot in finding_plots+fitting_plots],
    "reco/plots/{sample}/plots_{sample}.pdf"
  params:
    finding_plots = ','.join(finding_plots),
    fitting_plots = ','.join(fitting_plots)
  shell:
    "./with_env.sh python3 ./plot_basic_diagnostics.py {input} reco/plots/{wildcards.sample} --finding-plots {params.finding_plots} --fitting-plots {params.fitting_plots} --label {wildcards.sample}"
